+alias org.eolang.txt.sprintf
+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.0

# An assertion that two strings are equal to each other,
# used mostly for tests. This object is especially helpful
# in situations when it's not clear why a test fails.
[expected actual] > assert
  if. > @
    expected.eq actual
    true
    error
      if.
        and.
          expected.as-bytes.size.eq actual.as-bytes.size
          expected.as-bytes.size.eq 8
        sprintf
          "Expected %d but received %d"
          * expected actual
        sprintf
          "Expected \"%s\" but received \"%s\""
          * expected actual

  # A comparison of two equal string values.
  [] +> compares-two-equal-strings
    assert > @
      "hello"
      "hello"

  # A comparison of two equal boolean values.
  [] +> compares-two-booleans
    assert > @
      true
      5.gt 1
