+alias org.eolang.txt.sprintf
+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.0

# Assert that two strings are equal to each other (used
# mostly for tests). This object is especially helpful
# for situations when it's not clear why a test fails.
[expected actual] > assert
  if. > @
    expected.eq actual
    true
    error
      if.
        and.
          expected.as-bytes.size.eq actual.as-bytes.size
          expected.as-bytes.size.eq 8
        sprintf
          "Expected %d but received %d"
          * expected actual
        sprintf
          "Expected \"%s\" but received \"%s\""
          * expected actual

  # Compares two string values that are equal.
  assert +> compares-two-equal-strings
    "hello"
    "hello"

  # Compares two boolean values that are equal.
  assert +> compares-two-booleans
    true
    5.gt 1
