+alias eo3.parsing-lines.assert
+alias eo3.parsing-lines.fixed
+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.1

# An abstraction of a line formatted with tabular columns
# for each element in the `line` collection, with the `widths` provided.
[line widths] > tabular
  joined. > @
    QQ.txt.text " "
    mappedi.
      QQ.structs.list
        split.
          QQ.txt.text line
          ","
      [p i] >>
        fixed > @
          p
          widths.at i

  # Prints a simple line with tabular formatting.
  [] +> prints-simple-line
    assert > @
      "Joh... J... John Travolta   "
      tabular
        "John Malkovich,Johnny Depp,John Travolta"
        * 6 4 16

  # Prints lines from longer UTF-8 parts with various formatting.
  [] +> prints-line-from-longer-parts
    each. > @
      QQ.structs.list
        *
          *
            "L... A... 1844"
            "Le Comte de Monte-Cristo,Alexandre Dumas,1844"
            * 4 4 4
          *
            "Маст... Миха... 1928   "
            "Мастер и Маргарита,Михаил Афанасьевич Булгаков,1928"
            * 7 7 7
          *
            "Oscar Fingal O'l..."
            "Oscar Fingal O'ls Wilde"
            * 19
          *
            "Oscar Fingal O'ls... 1981"
            "Oscar Fingal O'ls Wilde,1981"
            * 20 4
          *
            "B... T... J... W... R..."
            "Brad Pitt,Tom Cruise,Johnny Depp,Will Smith,Robert DeNiro"
            * 4 4 4 4 4
          *
            "The Picture of Do... Oscar Fingal O'ls... 1891  "
            "The Picture of Dorian Gray,Oscar Fingal O'ls Wilde,1891"
            * 20 20 6
          *
            "The Picture of Do... Oscar Fingal O'ls Wilde  1891 "
            "The Picture of Dorian Gray,Oscar Fingal O'ls Wilde,1891"
            * 20 24 5
      [pair]
        assert > @
          pair.at 0
          tabular
            pair.at 1
            pair.at 2
