+alias eo3.parsing-lines.assert
+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.1

# A piece of text in a fixed-width box. If the text is larger
# than the width provided, it becomes shorter with
# an ellipsis at the end.
[txt width] > fixed
  if. > @
    txt.length.gt width
    QQ.txt.sprintf *1
      "%s..."
      txt.slice
        0
        width.minus 3
    if.
      txt.length.lt width
      chained.
        QQ.txt.text
          txt
        *
          repeated.
            QQ.txt.text
              " "
            width.minus txt.length
      txt

  # Makes the text shorter with an ellipsis at the end when it is too long.
  [] +> makes-text-shorter
    assert > @
      "Михаил Афанась..."
      fixed
        "Михаил Афанасьевич"
        17

  # Makes the text longer by filling the difference with spaces when it is too short.
  [] +> makes-text-longer
    assert > @
      "Булгаков    "
      fixed
        "Булгаков"
        12

  # Keeps the text the same size when it is exactly the required length.
  [] +> keeps-text-same-size
    assert > @
      "Дюма"
      fixed
        "Дюма"
        4

  # Makes empty text longer by filling it with spaces.
  [] +> makes-empty-text-longer
    assert > @
      "    "
      fixed
        ""
        4

  # Formats different cases with UTF-8 text parts.
  [] +> formats-different-cases
    * > cases
      *
        "Oscar Fingal O'ls..."
        "Oscar Fingal O'ls Wilde"
        20
      *
        "Мих..."
        "Михаил Афанасьевич Булгаков"
        6
    each. > @
      QQ.structs.list
        cases
      [case]
        assert > @
          case.at 0
          fixed
            case.at 1
            case.at 2
