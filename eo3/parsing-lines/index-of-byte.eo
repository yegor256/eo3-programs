+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.0

# In a byte array, find the index of the first occurrence of a given needle,
# or return -1 if not found. The needle is either a single byte or
# a byte array. The byte array is the first argument, and the needle is the second.
[bytes needle] > index-of-byte
  index-of-byte > next!
    bytes.slice 1 (bytes.size.minus 1)
    needle
  if. > @
    bytes.size.eq 0
    -1
    if.
      eq.
        bytes.slice 0 1
        needle
      0
      if.
        next.as-number.lt 0
        next.as-number
        next.as-number.plus 1

  # Finds a single byte where it exists.
  assert +> finds-one-byte
    6
    index-of-byte
      "Hello, друг!".as-bytes
      " ".as-bytes

  # Finds a single byte where it exists, at the first position.
  assert +> finds-first-byte
    0
    index-of-byte
      "John Malkovich".as-bytes
      "J".as-bytes

  # Finds the second byte.
  assert +> finds-second-byte
    1
    index-of-byte
      "John Malkovich".as-bytes
      "o".as-bytes

  # Returns negative when byte not found.
  lt. +> doesnt-find-anything
    index-of-byte
      "John Malkovich".as-bytes
      "\n".as-bytes
    0

  # Returns negative when byte array is empty.
  lt. +> doesnt-find-anything-in-empty-bytes
    index-of-byte
      --
      42-
    0
