+alias eo3.parsing-lines.assert
+alias eo3.parsing-lines.log
+alias eo3.parsing-lines.ring-buffer
+home https://github.com/yegor256/eo3-programs
+package eo3.parsing-lines
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.0

# A consumer of the input ring that passes it to the provided
# consumer. The consumer is an object that accepts
# the input buffer and the ring buffer as arguments. It
# reads the input buffer and updates the ring buffer accordingly. It
# returns the ring.
[input ring consumer] > consumed
  log > @
    "Consuming %d bytes, ring size is %d/%d (%d+%d)"
    *
      input.buffer.size
      ring.content.size
      ring.block.size
      ring.start
      ring.length
    if.
      and.
        input.buffer.size.eq 0
        ring.is-empty
      ring
      consumed
        input.read 16
        consumer
          with.
            ring
            input.buffer
        consumer

  # Reads simple stream and passes ring buffer to a simple consumer.
  [] +> consumes-short-stream
    assert > @
      0
      malloc.of
        64
        [m] >>
          size. > @
            content.
              consumed
                i.read 64
                ring-buffer.empty m
                [ring]
                  ring.beheaded > @
                    ring.length
    QQ.io.bytes-as-input > i
      "just a few words here".as-bytes

  # Reads simple stream and passes ring buffer to a simple consumer.
  [] +> consumes-long-stream
    malloc.for > @
      0
      [s] >>
        seq > @
          *
            malloc.of
              0
              [m] >>
                consumed > @
                  i.read 16
                  ring-buffer.empty m
                  [ring] >>
                    seq > @
                      *
                        s.put
                          s.as-number.plus
                            ring.length
                        ring.beheaded
                          ring.length
            assert
              i.bts.size
              s.as-number
    QQ.io.bytes-as-input > i
      """
      Lorem ipsum dolor sit amet, consectetur adipiscing elit.\n
      Duis sit amet tincidunt est.\n Phasellus condimentum dui at
      risus consequat gravida.\n Vestibulum risus tortor, vehicula
      id accumsan ut, laoreet vitae ex.\n Donec porta eleifend velit
      et rutrum.\n Nunc dapibus eu tellus nec facilisis.\n Name vulputate
      nunc elit, convallis consectetur quam lobortis eget.\n Class
      aptent taciti sociosqu ad litora torquent per conubia nostra,
      per inceptos himenaeos.\n
      """.as-bytes
