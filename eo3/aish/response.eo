+home https://github.com/yegor256/eo3-programs
+package eo3.aish
+spdx SPDX-FileCopyrightText: Copyright (c) 2022-2025 Yegor Bugayenko
+spdx SPDX-License-Identifier: MIT
+version 0.0.1

# HTTP response.
[input page] > response
  bytes-as-input > @
    joined.
      QQ.txt.text ""
      *
        QQ.txt.sprintf "HTTP/1.1 %d OK\r\n" * concrete.code
        mapped.
          QQ.structs.list
            concrete.headers-out
          QQ.txt.sprintf "%s: %s\r\n" * (h.at 0) (h.at 1) > [h]
        "\r\n"
        concrete.body-out
  input-as-bytes input > request!
  (QQ.txt.text request).split "\r\n\r\n" > parts'
  parts.at 0 > head-in!
  parts.at 1 > body-in!
  head-in.split "\r\n" > head'
  (head.at 0).split " " > crumbs'
  crumbs.at 0 > method!
  crumbs.at 1 > path!
  mapped. > headers-in'
    QQ.structs.list
      head.slice 1 head.length
    ln.split ": " > [ln]
  page method path headers-in body-in > concrete
